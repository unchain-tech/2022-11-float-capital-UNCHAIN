// SPDX-License-Identifier: BUSL-1.1

pragma solidity 0.8.17;

import "../interfaces/IRegistry.sol";
import "../interfaces/IGEMS.sol";
import "../interfaces/IMarket.sol";
import "../interfaces/IMarketExtended.sol";
import "../interfaces/IOracleManager.sol";

library OracleManagerUtils {
  //// Types:
  // NOTE: this struct is used to reduce stack usage and fix coverage.
  // it does use more gas though :/ Coverage is more important than gas optimization currently.
  struct MissedEpochExecution {
    bool _isSearchingForuint80;
    uint80 _currentOracleRoundId;
    uint32 _currentMissedEpochPriceUpdatesArrayIndex;
  }

  function _shouldOracleUpdateExecuteEpoch(
    IOracleManager oracleManager,
    uint256 currentEpochStartTimestamp,
    uint256 previousOracleUpdateTimestamp,
    uint256 currentOracleUpdateTimestamp
  ) internal view returns (bool) {
    //Don't use price for execution because MEWT has not expired yet
    //current price update epoch is ahead of MEWT so we check if the previous value
    //occurred before MEWT to validate that this is the correct price update to use

    //  first condition checks for whether the oracle price update occurs before Minimum Execution Wait Threshold is expired.
    return
      (previousOracleUpdateTimestamp < currentEpochStartTimestamp + oracleManager.MINIMUM_EXECUTION_WAIT_THRESHOLD()) &&
      (currentOracleUpdateTimestamp >= currentEpochStartTimestamp + oracleManager.MINIMUM_EXECUTION_WAIT_THRESHOLD());
  }

  /// @notice Calculates number of epochs which have missed system state update, due to bot failing
  /// @dev Called by internal function to decide how many epoch execution info (oracle price update details) should be returned
  /// @dev It is "maximum" as this is just the upper
  /// @param _latestExecutedEpochIndex index of the most recently executed epoch
  function _getMaximumNumberOfMissedEpochs(
    IOracleManager oracleManager,
    uint256 _latestExecutedEpochIndex,
    uint256 latestOraclePriceUpdateTime
  ) internal view returns (uint256 _numberOfMissedEpochs) {
    _numberOfMissedEpochs = oracleManager.getCurrentEpochIndex() - _latestExecutedEpochIndex - 1;

    if (_numberOfMissedEpochs == 0) return 0;

    // Checks for whether the oracle price update occurs before Minimum Execution Wait Threshold is expired.
    if (latestOraclePriceUpdateTime < oracleManager.getEpochStartTimestamp() + oracleManager.MINIMUM_EXECUTION_WAIT_THRESHOLD()) {
      _numberOfMissedEpochs -= 1;
    }
  }

  /// @notice returns an array of info on each epoch price update that was missed
  /// @dev This function gets executed in a system update on the market contract
  /// @param _latestExecutedEpochIndex the most recent epoch index in which a price update has been executed
  /// @param _previousOracleUpdateIndex the "roundId" used to reference the most recently executed oracle price on chainlink
  function getMissedEpochPriceUpdates(
    IOracleManager oracleManager,
    uint32 _latestExecutedEpochIndex,
    uint80 _previousOracleUpdateIndex,
    uint256 _numberOfUpdatesToTryFetch
  ) public view returns (uint80[] memory _missedEpochOracleRoundIds) {
    AggregatorV3Interface chainlinkOracle = oracleManager.chainlinkOracle();
    AggregatorV3InterfaceS.LatestRoundData memory latestRoundData = AggregatorV3InterfaceS(address(chainlinkOracle)).latestRoundData();

    // check whether latestRoundData.startedAt is before end point of previous epoch
    // if met, then break
    if (oracleManager.getEpochStartTimestamp() - oracleManager.EPOCH_LENGTH() > latestRoundData.startedAt) {
      _missedEpochOracleRoundIds = new uint80[](0);

      return (_missedEpochOracleRoundIds);
    }
    uint256 _numberOfMissedEpochs = Math.min(
      _getMaximumNumberOfMissedEpochs(oracleManager, _latestExecutedEpochIndex, latestRoundData.startedAt),
      _numberOfUpdatesToTryFetch
    );

    _missedEpochOracleRoundIds = new uint80[](_numberOfMissedEpochs);

    if (_numberOfMissedEpochs == 0) {
      return (_missedEpochOracleRoundIds);
    }

    MissedEpochExecution memory _missedEpochExecution = MissedEpochExecution({
      _isSearchingForuint80: true,
      _currentOracleRoundId: _previousOracleUpdateIndex + 1,
      _currentMissedEpochPriceUpdatesArrayIndex: 0
    });

    //  Start at the timestamp of the first epoch index after the latest executed epoch index
    // We add 1 to get the end timestamp of the latest executed epoch, then another 1 to get the next epoch, hence we add 2.
    latestRoundData.startedAt = (uint256(_latestExecutedEpochIndex) + 2) * oracleManager.EPOCH_LENGTH() + oracleManager.initialEpochStartTimestamp();

    // Called outside of the loop and then updated on each iteration within the loop
    (, , uint256 _previousOracleUpdateTimestamp, , ) = chainlinkOracle.getRoundData(_previousOracleUpdateIndex);

    while (_missedEpochExecution._isSearchingForuint80 && latestRoundData.roundId >= _missedEpochExecution._currentOracleRoundId) {
      (, , uint256 _currentOracleUpdateTimestamp, , ) = chainlinkOracle.getRoundData(_missedEpochExecution._currentOracleRoundId);

      // Check if there was a 'phase change' AND the `_currentOracleUpdateTimestamp` is zero.
      if ((latestRoundData.roundId >> 64) != (_previousOracleUpdateIndex >> 64) && _currentOracleUpdateTimestamp == 0) {
        // NOTE: if the phase changes, then we want to correct the phase of the update.
        //       There is no guarantee that the phaseID won't increase multiple times in a short period of time (hence the while loop).
        //       But chainlink does promise that it will be sequential.
        while (_currentOracleUpdateTimestamp == 0) {
          _missedEpochExecution._currentOracleRoundId =
            (((_missedEpochExecution._currentOracleRoundId >> 64) + 1) << 64) |
            uint80(uint64(_missedEpochExecution._currentOracleRoundId));

          (, , _currentOracleUpdateTimestamp, , ) = chainlinkOracle.getRoundData(_missedEpochExecution._currentOracleRoundId);
        }
      }
      if (_shouldOracleUpdateExecuteEpoch(oracleManager, latestRoundData.startedAt, _previousOracleUpdateTimestamp, _currentOracleUpdateTimestamp)) {
        // check whether oracle update is after end point of next epoch
        // if met, break the loop and send back the false
        // Checks for whether the oracle price update happened before end of current epoch end timestamp
        if (_currentOracleUpdateTimestamp > latestRoundData.startedAt + oracleManager.EPOCH_LENGTH()) {
          uint80[] memory truncatedMissedEpochOracleRoundIds = new uint80[](_missedEpochExecution._currentMissedEpochPriceUpdatesArrayIndex);
          for (uint256 i = 0; i < _missedEpochExecution._currentMissedEpochPriceUpdatesArrayIndex; ++i) {
            truncatedMissedEpochOracleRoundIds[i] = _missedEpochOracleRoundIds[i];
          }
          return (truncatedMissedEpochOracleRoundIds);
        } else {
          _missedEpochOracleRoundIds[_missedEpochExecution._currentMissedEpochPriceUpdatesArrayIndex] = _missedEpochExecution._currentOracleRoundId;
        }

        // Increment to the next array index and the correct timestamp
        _missedEpochExecution._currentMissedEpochPriceUpdatesArrayIndex += 1;
        latestRoundData.startedAt += uint32(oracleManager.EPOCH_LENGTH());

        // Check that we have retrieved all the missed epoch updates that we are searching
        // for and end the while loop
        if (_missedEpochExecution._currentMissedEpochPriceUpdatesArrayIndex == _numberOfMissedEpochs) {
          _missedEpochExecution._isSearchingForuint80 = false;
        }
      }

      //Previous oracle update timestamp can be reassigned to the current for the next iteration
      _previousOracleUpdateTimestamp = _currentOracleUpdateTimestamp;
      ++_missedEpochExecution._currentOracleRoundId;
    }
  }

  /// @notice Returns oracle information for executing historical epoch(s)
  /// @param latestExecutedEpochIndex the most recent epoch index in which a price update has been executed
  /// @param latestExecutedOracleRoundId the "roundId" used to reference the most recently executed oracle price on chainlink
  /// @return missedEpochOracleRoundIds list of epoch execution information
  function getOracleInfoForSystemStateUpdate(
    IOracleManager oracleManager,
    uint32 latestExecutedEpochIndex,
    uint80 latestExecutedOracleRoundId
  ) external view returns (uint80[] memory missedEpochOracleRoundIds) {
    uint256 numberOfEpochsSinceLastEpoch = (oracleManager.getCurrentEpochIndex() - latestExecutedEpochIndex) * oracleManager.EPOCH_LENGTH();

    // If the oracle falls more than 6 epochs behind it will only return 6 of them (but catch up 6 at a time).
    //      And 30 for mumbai (because it can handle bigger transactions)
    if (numberOfEpochsSinceLastEpoch > (6 * (block.chainid == 80001 ? 5 : 1))) {
      numberOfEpochsSinceLastEpoch = 6 * (block.chainid == 80001 ? 5 : 1);
    }

    missedEpochOracleRoundIds = getMissedEpochPriceUpdates(
      oracleManager,
      latestExecutedEpochIndex,
      latestExecutedOracleRoundId,
      numberOfEpochsSinceLastEpoch
    );
  }

  /// @notice Returns start timestamp of current epoch
  /// @return getEpochStartTimestamp start timestamp of the current epoch
  function getEpochStartTimestamp(IOracleManager oracleManager, uint32 epochIndex) external view returns (uint256) {
    return (uint256(epochIndex) * oracleManager.EPOCH_LENGTH()) + oracleManager.initialEpochStartTimestamp();
  }
}
